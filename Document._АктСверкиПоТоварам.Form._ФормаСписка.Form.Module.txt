
//
&НаСервере
Процедура СформироватьАктСверкиКонтрагентСервер(Контрагент, ДатаСверки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	_АктСверкиПоДоговорамХранения.Ссылка
	|ИЗ
	|	Документ._АктСверкиПоДоговорамХранения КАК _АктСверкиПоДоговорамХранения
	|ГДЕ
	|	_АктСверкиПоДоговорамХранения.ДатаСверки = &ДатаСверки
	|	И _АктСверкиПоДоговорамХранения.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("ДатаСверки", НачалоДня(ДатаСверки));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		Документ = Документы._АктСверкиПоДоговорамХранения.СоздатьДокумент();
	Иначе
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Документ = Выборка.Ссылка.ПолучитьОбъект();	
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Документ.Дата       = ТекущаяДата();
	Документ.ДатаСверки = ДатаСверки;
	Документ.Контрагент = Контрагент;
	
	Документы._АктСверкиПоДоговорамХранения.ЗаполнитьДокументСервер(Документ);
	
	Документ.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

//
&НаСервере
Процедура СформироватьАктыНаДатуСервер(ДатаСверки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	_ТоварыНаХраненииОстатки.Склад._Контрагент КАК Контрагент
	|ИЗ
	|	РегистрНакопления._ТоварыНаХранении.Остатки(&ДатаСверки, Склад._СкладОтветственногоХранения = ИСТИНА) КАК _ТоварыНаХраненииОстатки";
	
	Запрос.УстановитьПараметр("ДатаСверки", ДатаСверки);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СформироватьАктСверкиКонтрагентСервер(Выборка.Контрагент, ДатаСверки);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВсеАкты(Команда)
	
	ДатаСверки = ТекущаяДата();
	Если ВвестиДату(ДатаСверки, "Введите дату сверки", ЧастиДаты.Дата) Тогда 
		СформироватьАктыНаДатуСервер(ДатаСверки);
	КонецЕсли;
	
КонецПроцедуры
